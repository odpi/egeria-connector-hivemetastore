/*
 * SPDX-License-Identifier: Apache-2.0
 * Copyright Contributors to the ODPi Egeria project.
 */

plugins {
    id 'java-library'
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id 'idea'
    id 'maven-publish'
}

if (System.getenv("CI")) {
    apply plugin: 'signing'
}

repositories {
    mavenCentral()
    maven { url("https://oss.sonatype.org/content/repositories/snapshots") }
    mavenLocal() // note - this may pick up modified classes from ~/.m2
}

// ensures we pick up the very latest snapshots when built
configurations.all {
    // check for updates every build
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}
configurations.all {
    exclude group: 'org.slf4j', module: 'slf4j-log4j12'


//    exclude group:'org.apache.logging.log4j', module: 'log4j'
//    exclude group: 'org.apache.logging.log4j', module: 'log4j-slf4j-impl'
}

group = 'org.odpi.egeria'
version = '1.0-SNAPSHOT'
ext.name = 'New Egeria code'
description = 'New code for Egeria'

ext {
    egeriaversion = '3.8'
    slf4jVersion = '1.7.36'
    jacksonVersion = '2.13.2'
    jupiterVersion = '5.8.2'
    httpclientVersion = '4.5.13'
    springwebVersion = '5.3.19'
    hiveversion = '3.1.3'
}

// For a single module, add here. For multiple modules, refactor and use constraints (see main egeria build.gradle for an example)
dependencies {

    // Only used for build - already present in server chassis at runtime - similar to 'provided' with maven
    compileOnly "org.odpi.egeria:audit-log-framework:${egeriaversion}"

    // Needed for build and run
    implementation "org.slf4j:slf4j-api:${slf4jVersion}"


    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    implementation "com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}"
    implementation "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
    implementation "org.springframework:spring-core:${springwebVersion}"
    implementation "org.junit.jupiter:junit-jupiter:${jupiterVersion}"
    implementation "org.odpi.egeria:open-connector-framework:${egeriaversion}"
    implementation "org.odpi.egeria:open-metadata-types:${egeriaversion}"
    implementation "org.odpi.egeria:audit-log-framework:${egeriaversion}"
    implementation "org.odpi.egeria:repository-services:${egeriaversion}"
    implementation "org.odpi.egeria:repository-services-implementation:${egeriaversion}"
    implementation "org.odpi.egeria:admin-services-api:${egeriaversion}"
    implementation "org.odpi.egeria:connector-configuration-factory:${egeriaversion}"
    implementation "org.odpi.egeria:repository-services-apis:${egeriaversion}"
    implementation "org.odpi.egeria:admin-services-api:${egeriaversion}"
    implementation "org.odpi.egeria:audit-log-framework:${egeriaversion}"
    // Hive
//    implementation 'org.apache.thrift:libthrift:0.9.3'
//    implementation 'org.apache.thrift:libfb303:0.9.3'
//    implementation "org.apache.hive:hive-common:3.1.2"
    implementation "org.apache.hadoop:hadoop-common:3.3.3"
    // local patched HMS Client - including patched log4j
    implementation(files('libs/hive-metastore-standalone-client-3.1.2-sqlquery.jar'))
//    implementation 'org.apache.hive:hive-standalone-metastore:3.1.3'
    // Only needed to compile test code
    testCompileOnly "org.junit.jupiter:junit-jupiter-api:${jupiterVersion}"
    testImplementation "org.odpi.egeria:topic-integrator-api:${egeriaversion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${jupiterVersion}"

}


test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    reports {
        html.required = true
    }
    filter {
        includeTestsMatching "*Test"
    }
}


// Maven Central (technically sonatype oss) requires we distribute source and javadoc.
java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    withJavadocJar()
    withSourcesJar()
}

// More Java language settings
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    sourceCompatibility = "11"
    targetCompatibility = "11"
    options.incremental = true
    options.fork = true
    options.failOnError = true
    options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'
}

// For later java versions this is recommended - keep conditional in case we want to build on 8
javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

// We will typically build an uber jar with dependencies. Can change name here.
shadowJar {
    archiveClassifier = 'jar-with-dependencies'
    // required as this hits a jar limit otherwise
    zip64 true
}

// We want to built a jar with dependencies in a normal build, so to avoid specifying task explicitly
build.dependsOn shadowJar

// We only have a single artifact for now - this additional metadata is
// required for publishing to maven central. Only doing signing in 'CI'
publishing {
    publications {
        connector(MavenPublication) {
            from components.java
            pom {
                url = 'http://egeria.odpi.org'
                licenses {
                    // Code
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                    // Docs
                    license {
                        name = 'Creative Commons Attribution 4.0 International (CC BY 4.0)'
                        url = 'https://creativecommons.org/licenses/by/4.0'
                    }
                }
                developers {
                    developer {
                        id = 'planetf1'
                        name = 'Nigel Jones'
                        email = 'nigel.l.jones+git@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/odpi/egeria-connector-repository-file-sample.git'
                    developerConnection = 'scm:git:ssh://github.com/odpi/egeria-connector-repository-file-sample.git'
                    url = 'http://github.com/odpi/egeria-connector-repository-file-sample/tree/main'
                }
            }
            // Override the project name & description for the pom based on properties set in the child build.gradle (hard to default & required for maven central)
            pom.withXml {
                asNode().appendNode('name', "${project.ext.name}")
                asNode().appendNode('description', "${project.description}")
            }
        }
    }

    // Release versions get pushed to staging area on maven central, snapshots to snapshot repo
    // Secrets for credentials
    if (System.getenv("CI")) {
        repositories {
            maven {
                name = 'OSSRH'
                def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
                def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                // User token (under profile) on oss.sonatype.org
                credentials {
                    username = System.getenv("OSSRH_USERNAME")
                    password = System.getenv("OSSRH_TOKEN")
                }
            }
        }
    }
}

// To publish to ossrh we need to sign the artifacts - only in CI
if (System.getenv("CI")) {
    signing {
        // This is the publication to sign
        sign publishing.publications.connector
        // gpg --export-secret-keys myemal@gmail.com | base64
        def signingKey = System.getenv("OSSRH_GPG_PRIVATE_KEY")
        // Passphrase for key
        def signingPassword = System.getenv("OSSRH_GPG_PASSPHRASE")
        // public key id (last 8 characters only) - note keys also need uploading to all the main registries
        def signingKeyId = System.getenv("OSSRH_GPG_KEYID")
        // We use these values from secrets rather than gradle.properties
        useInMemoryPgpKeys(signingKeyId, signingKey, signingPassword)
    }
}
